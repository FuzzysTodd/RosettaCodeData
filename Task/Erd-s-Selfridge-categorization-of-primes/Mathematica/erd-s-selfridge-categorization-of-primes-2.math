ClearAll[ErdosSelfridgeCategory, CategorySizesTable, categories];

ErdosSelfridgeCategory[n_] := ErdosSelfridgeCategory[n] =
   If[ Last[primesToCheck = FactorInteger[n + 1][[All, 1]]] <= 3,
    1,
    1 + Max[ErdosSelfridgeCategory /@ primesToCheck]
    ];

(* Display the first 200 primes allocated to their category *)

categories = GatherBy[Prime[Range[200]], ErdosSelfridgeCategory];

Print["First 200 primes by Erdös-Selfridge category:\n"]

Do[
    Print["Class ", i, ":"];
    Print[
        TableForm[
            Partition[
                Map[StringPadLeft[ToString[#], 5] &, categories[[i]]], UpTo[12]],
        TableAlignments -> Right]
    ],
    {i, Length[categories]}
];

(* Show the number of primes in each category for the first million primes *)

Print["Calculating Erdős-Selfridge categories for the first million primes...."]

{seconds, categories} =
  AbsoluteTiming[GatherBy[Prime[Range[10^6]], ErdosSelfridgeCategory]];

Print["Calculation took ", seconds " seconds."];

CategorySizesTable[categories_List] :=
  TableForm[
   Table[{Min[categories[[n]]], Max[categories[[n]]],
     Length[categories[[n]]]}, {n, Length[categories]}],
   TableHeadings -> {Automatic, {"First", "Last", "Count"}},
   TableAlignments -> Right];

Print[CategorySizesTable[categories]]
