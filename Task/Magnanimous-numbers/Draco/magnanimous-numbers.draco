proc isprime(word n) bool:
    word d;
    bool prime;
    if   n<2   then false
    elif n%2=0 then n=2
    elif n%3=0 then n=3
    else
        prime := true;
        d := 5;
        while prime and d*d <= n do
            if n%d=0 then prime := false fi;
            d := d+2;
            if n%d=0 then prime := false fi;
            d := d+4
        od;
        prime
    fi
corp

proc magnanimous(word n) bool:
    word left, right, shift;
    bool magn;
    left := n;
    right := 0;
    shift := 1;
    magn := true;
    while magn and left >= 10 do
        right := right + (left % 10) * shift;
        shift := shift * 10;
        left := left / 10;
        magn := magn and isprime(left + right)
    od;
    magn
corp

proc main() void:
    word n, i;
    n := 0;
    for i from 1 upto 250 do
        while not magnanimous(n) do n := n+1 od;
        if i=1 then writeln("1 - 45:") fi;
        if i=241 then writeln("241 - 250:") fi;
        if i<=45 or i>=241 then
            write(n:7);
            if i%5 = 0 then writeln() fi
        fi;
        n := n+1
    od
corp
