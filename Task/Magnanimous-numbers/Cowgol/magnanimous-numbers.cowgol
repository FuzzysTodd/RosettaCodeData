include "cowgol.coh";

sub prime(n: uint32): (r: uint32) is
    r := 0;
    if n <= 4 or n & 1 == 0 or n % 3 == 0 then
        if n == 2 or n == 3 then
            r := 1;
        end if;
        return;
    end if;

    var d: uint32 := 5;
    while d*d <= n loop
        if n % d == 0 then return; end if;
        d := d+2;
        if n % d == 0 then return; end if;
        d := d+4;
    end loop;
    r := 1;
end sub;

sub magnanimous(n: uint32): (r: uint32) is
    r := 1;
    var left: uint32 := n;
    var right: uint32 := 0;
    var shift: uint32 := 1;

    while left >= 10 loop
        right := right + (left % 10) * shift;
        shift := shift * 10;
        left := left / 10;
        if prime(left + right) == 0 then
            r := 0;
            break;
        end if;
    end loop;
end sub;

var i: uint16 := 0;
var n: uint32 := 0;

while i <= 400 loop
    while magnanimous(n) == 0 loop n := n+1; end loop;
    i := i + 1;

    if i == 1 then print("1 - 45:\n");
    elseif i == 241 then print("241 - 250:\n");
    elseif i == 391 then print("390 - 400:\n");
    end if;

    if i<=45 or (i>240 and i<=250) or (i>390 and i<=400) then
        print_i32(n);
        if i % 5 == 0 then print_nl();
        else print_char('\t');
        end if;
    end if;
    n := n + 1;
end loop;
