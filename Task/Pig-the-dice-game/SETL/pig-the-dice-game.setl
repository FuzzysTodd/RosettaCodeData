program pig;
    setrandom(0);
    scores := {[1,0], [2,0]};

    t := 1;
    loop until exists n = scores(p) | n >= 100 do
        print("Current score: player 1 = " + str scores(1) +
              ", player 2 = " + str scores(2));
        scores(t) +:= turn(t);
        t := 3-t;
    end loop;

    print("Player " + str p + " wins!");
    print("Final score: player 1 = " + str scores(1) +
              ", player 2 = " + str scores(2));

    proc turn(p);
        score := 0;
        loop do
            putchar("Player " + str p + " - " + str score + " - R)oll or H)old? ");
            loop doing
                flush(stdout);
                choice := to_upper getline(stdin);
            while choice notin {"R","H"} do
                putchar("Invalid input. R)oll or H)old? ");
            end loop;

            if choice = "H" then
                print("Player " + str p + "'s turn ends with " + str score + " points.");
                return score;
            end if;

            if (die := roll()) = 6 then
                print("You rolled a 6. You lose your points and your turn ends.");
                return 0;
            end if;

            print("You rolled a " + str die + ".");
            score +:= die;
        end loop;
    end proc;

    proc roll();
        return 1 + random(5);
    end proc;
end program;
