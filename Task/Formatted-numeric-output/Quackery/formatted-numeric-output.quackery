  [ over find swap found ]     is has       (   $ c --> b )

  [ over 0 peek
    char - = iff
      [ dip [ behead drop ]
        true unrot ]
    else [ false unrot ]
    over char . swap find
    -  char 0 swap of
    swap join
    swap iff char -
    else space
    swap join ]                is left-pad  (   $ n --> $ )

   [ over char . has not if
       [ dip [ char . join ] ]
     over char . swap find
     dip [ over size ]
     1+ - -
     char 0 swap of
     join ]                    is right-pad (   $ n --> $ )

  [ dip left-pad right-pad ]   is format    ( $ n n --> $ )

  ' [ $ "7.125"
      $ "-7.125"
      $ "0.125"
      $ "-0.12"
      $ "7.12"
      $ "-7.12"
      $ "0.12"
      $ "-0.12"
      $ "7"
      $ "-7"
      $ "0" ]
  witheach
    [ do 5 3 format echo$ cr ]
