100 REM Trabb Pardo-Knuth algorithm
110 REM Used "magic numbers" because of strict specification of the algorithm.
120 DEF FNF(N) = SQR(ABS(N)) + 5 * N * N * N
130 DIM S(0 TO 10)
140 PRINT "Enter 11 numbers."
150 FOR I = 0 TO 10
160    INPUT PROMPT STR$(I + 1) & ":": S(I)
170 NEXT I
180 PRINT
190 REM Reverse
200 FOR I = 0 TO INT(10 / 2)
210    LET Tmp = S(I)
220    LET S(I) = S(10 - I)
230    LET S(10 - I) = Tmp
240 NEXT I
250 REM Results
260 FOR I = 0 TO 10
270    LET R = FNF(S(I))
280    PRINT USING "f(####.###) = ": S(I);
290    IF R > 400 THEN
300       PRINT "overflow"
310    ELSE
320       PRINT USING "####.###": R
330    END IF
340 NEXT I
350 END
