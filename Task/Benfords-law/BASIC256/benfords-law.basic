n = 1000
dim actual(n) fill 0

for nr = 1 to n
	num$ = string(fibonacci(nr))
	j = int(left(num$,1))
	actual[j] += 1
next

print "First 1000 Fibonacci numbers"
print "Digit  ", "Actual freq  ", "Expected freq"
for i = 1 to 9
	freq = frequency(i)*100
	print "  "; ljust(i,4), rjust(actual[i]/10,5), rjust(freq,5)
next
end

function frequency(n)
	return (log10(n+1) - log10(n))
end function

function fibonacci(f)
	f = int(f)
	a = 0 : b = 1 : c = 0 : n = 0

	while n < f
		a = b
		b = c
		c = a + b
		n += 1
	end while

	return c
end function
