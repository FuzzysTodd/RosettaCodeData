 10 I$ = "WWWWWWWWWWWWBWWWWWWWWWWWWBBBWWWWWWWWWWWWWWWWWWWWWWWWBWWWWWWWWWWWWWW"
 20  GOSUB 100ENCODE
 30  GOSUB 200DECODE
 40  PRINT "INPUT: ";I$
 50  PRINT "OUTPUT: ";
 60  GOSUB 250 PRINT
 70  END
 100 O$ =  MID$ (I$,1,1):N$ =  MID$ ( CHR$ (0),1, LEN (O$)): IF  LEN (I$) < 2 THEN  RETURN
 110  FOR I = 2 TO  LEN (I$):C$ =  MID$ (I$,I,1): IF C$ <  >  RIGHT$ (O$,1) THEN O$ = O$ + C$:N$ = N$ +  CHR$ (0): NEXT I: RETURN
 120 N$ =  MID$ (N$,1, LEN (O$) - 1) +  CHR$ ( ASC ( MID$ (N$, LEN (O$))) + 1): NEXT I: RETURN
 200 I$ = "": IF  LEN (O$) THEN  FOR I = 1 TO  LEN (O$): FOR J = 0 TO  ASC ( MID$ (N$,I)):I$ = I$ +  MID$ (O$,I,1): NEXT J,I
 210  RETURN
 250  IF  LEN (O$) THEN  FOR I = 1 TO  LEN (O$): PRINT  ASC ( MID$ (N$,I)) + 1; MID$ (O$,I,1);: NEXT I
 260  RETURN
