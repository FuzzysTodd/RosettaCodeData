with javascript_semantics
atom t0 = time()
function twin_primes(integer maxp, bool both=true)
    integer n = 0,  -- result
            pn = 2, -- next prime index
            p,      -- a prime, <= maxp
            prev_p = 2
    while true do
        p = get_prime(pn)
        if both and p>=maxp then exit end if
        n += (prev_p = p-2)
        if (not both) and p>=maxp then exit end if
        prev_p = p
        pn += 1
    end while
    return n
end function
integer mp = 6 -- prompt_number("Enter limit:")
printf(1,"Twin prime pairs less than %,d: %,d\n",{mp,twin_primes(mp)})
printf(1,"Twin prime pairs less than %,d: %,d\n",{mp,twin_primes(mp,false)})
for p=1 to 9 do
    integer p10 = power(10,p)
    printf(1,"Twin prime pairs less than %,d: %,d\n",{p10,twin_primes(p10)})
end for
?elapsed(time()-t0)
