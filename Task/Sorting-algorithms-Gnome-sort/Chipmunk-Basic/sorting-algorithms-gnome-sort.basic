100 RANDOMIZE TIMER
110 DIM array(18)
120 ' Init Array
130 FOR i = 0 TO UBOUND(array)
140   array(i) = INT(RND(1)*98)+1
150 NEXT i
160 PRINT "unsort: "; : GOSUB 200
170 GOSUB 260 : ' gnomeSort
180 PRINT "  sort: "; : GOSUB 200
190 END
200 ' Write Array
210 FOR i = 0 TO UBOUND(array)
220   PRINT array(i);
230 NEXT i
240 PRINT
250 RETURN
260 ' gnomeSort
270   i = 1
280   j = i+1
290   WHILE i <= UBOUND(array)
300     IF array(i-1) <= array(i) THEN
310       i = j : j = j+1
320     ELSE
330       t = array(i-1) : array(i-1) = array(i) : array(i) = t : ' swap
340       i = i-1
350       IF i = 0 THEN i = j : j = j+1
360     ENDIF
370   WEND
380 RETURN
