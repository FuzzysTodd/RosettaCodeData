isRhonda(n, b) =
{
    local(mydigits, product, mysum, factors, pairProduct);

    mydigits = digits(n, b);
    product = vecprod(mydigits);
    factors = factor(n);
    mysum= 0;
    for(i = 1, matsize(factors)[1],
        pairProduct = factors[i, 1] * factors[i, 2];
        mysum += pairProduct;
    );
    product == b * mysum;
}

displayrhondas(low, high, nshow) =
{
    local(b, n, rhondas, count, basebRhondas);
    for(b = low, high,
        if(isprime(b), next);
        n = 1; rhondas = [];
        count = 0;
        while(count < nshow,
            if(isRhonda(n, b),
                rhondas = concat(rhondas, n);
                count++;
            );
            n++;
        );
        print("First " nshow " Rhondas in base " b ":");
        print("In base 10: " rhondas);
        basebRhondas = vector(#rhondas, i, (digits(rhondas[i], b)));
        print("In base " b ": " basebRhondas);
        print("\n");
    );
}

displayrhondas(2, 16, 15);
