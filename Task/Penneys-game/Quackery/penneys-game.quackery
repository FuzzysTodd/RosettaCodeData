  [ 2 random iff char H else char T ]   is flip         (   --> c )

  [ 3 times flip join join ]            is randstart    (   --> $ )

  [ -1 split drop
    dup -1 peek
    char T = iff char H else char T
    swap join ]                         is beststart    ( $ --> $ )

  [ stack ]                             is playergoal   (   --> s )
  [ stack ]                             is computergoal (   --> s )

  [ say "This is Penney's game."
    cr cr
    say "We each guess a different "
    say "sequence of three coin "
    say "flips."
    cr
    say "I will releatedly flip "
    say "a coin until one of the "
    say "sequences happens."
    cr
    say "The winner is the one who "
    say "guesses the sequence that "
    say "happens first."
    cr cr
    say "Please enter your sequence "
    say "carefully as there is no "
    say "error checking."
    cr
    say 'For example, for "heads '
    say 'tails heads" type "HTH".'
    cr cr ]                              is intro       (   -->   )

  [ intro
    randomise
    flip char H = iff
      [ say "I start. My guess is: "
        randstart dup echo$
        computergoal put
        cr
        $ "Please enter your guess: "
        input playergoal put ]
    else
      [ say "You start. "
        $ "Please enter your guess: "
        input dup playergoal put
        beststart dup
        say "I guess: " echo$
        computergoal put
        cr ]
    cr
    $ ""
    [ flip join
      dup -3 split nip dup
      computergoal share = iff
        [ drop say "I win." ] done
      playergoal share = iff
        [ say "You win." ] done
      again ]
    cr
    say "The complete sequence was: "
    echo$ cr
    computergoal release
    playergoal release ]               is play          (   -->   )
