( ( median
  =     begin decimals end int list
      , med med1 med2 num number
    .   ( convertToRational
        =
          .     new$(UFP,'(.$arg:?V))
              : ?ufp
            & (ufp..go)$
            & (ufp..export)$(Q.V)
        )
      & 0:?list
      &   whl
        ' ( !arg:%?number ?arg
          & convertToRational$!number:?rationalnumber
          & (!rationalnumber.)+!list:?list
          )
      & !list:?+[?end
      & (   !end*1/2:~/
          &   !list
            :   ?
              + [!(=1/2*!end+-1)
              + (?med1.?)
              + (?med2.?)
              + ?
          & !med1*1/2+!med2*1/2:?med
        | !list:?+[(div$(1/2*!end,1))+(?med.)+?
        )
      & (new$(UFP,'(.$med)).go)$
  )
& out$(median$("4.1" 4 "1.2" "6.235" "7868.33"))
&   out
  $ ( median
    $ ( "4.4"
        "2.3"
        "-1.7"
        "7.5"
        "6.6"
        "0.0"
        "1.9"
        "8.2"
        "9.3"
        "4.5"
      )
    )
& out$(median$(1 5 3 2 4))
& out$(median$(1 5 3 6 4 2))
);
