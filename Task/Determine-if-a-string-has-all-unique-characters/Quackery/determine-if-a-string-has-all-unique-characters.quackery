  [ over find swap found ]    is has         ( $ c --> b     )

  [ dip [ 0 0 true ]
    dup size 2 < iff
      drop done
    dup size 1 - times
      [ behead 2dup has iff
          [ swap find
            dip not
            2swap 2drop
            i^ tuck + 1+ rot
            0 conclude ] done
        drop ]
    drop ]                    is uniquechars (   $ --> n n b )

  [ dup say 'String "'
    echo$
    say '" has length '
    dup size echo
    say ". "
    dup uniquechars iff
      [ say "There are no duplicated characters."
        drop 2drop ]
    else
      [ rot over peek
        dup say 'The character "'
        emit
        say '" (hex:'
        16 base put
        echo
        base release
        say ") is at positions "
        swap echo
        say " and "
        echo
        say "." ]
    cr ]                       is task        (   $ -->       )

  $ "" task
  $ "." task
  $ "abcABC" task
  $ "XYZ ZYX" task
  $ "1234567890ABCDEFGHIJKLMN0PQRSTUVWXYZ" task
