$ENTRY Go {
    = <Each Show <Iota 1 15> 2144>;
};

Factorize {
    1 = 1;
    s.N = <Factorize 2 s.N>;
    s.D s.N, <Compare s.N s.D>: '-' = ;
    s.D s.N, <Divmod s.N s.D>: {
        (s.R) 0 = s.D <Factorize s.D s.R>;
        e.X = <Factorize <+ 1 s.D> s.N>;
    };
};

Join {
    (e.J) = ;
    (e.J) s.N = <Symb s.N>;
    (e.J) s.N e.X = <Symb s.N> e.J <Join (e.J) e.X>;
};

Iota {
    s.End s.End = s.End;
    s.Start s.End = s.Start <Iota <+ s.Start 1> s.End>;
};

Each {
    s.F = ;
    s.F t.I e.X = <Mu s.F t.I> <Each s.F e.X>;
};

Show {
    e.N = <Prout <Symb e.N> ' = ' <Join (' x ') <Factorize e.N>>>;
};
