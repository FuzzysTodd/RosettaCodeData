program number_reversal_game;
    setrandom(0);
    tries := 0;
    state := shuffled_numbers();

    loop until state = "123456789" do
        tries +:= 1;
        swapat := read_step(tries, state);
        state := reverse state(..swapat) + state(swapat+1..);
    end loop;
    print(state + " - You win in " + str tries + " tries.");

    proc read_step(tries, state);
        loop until r in [str d : d in [1..9]] do
            putchar(state + " - Reverse how many? ");
            flush(stdout);
            r := getline(stdin);
        end loop;
        return val r;
    end proc;

    proc shuffled_numbers();
        digits := "123456789";
        loop until out /= digits do
            dset := {d : d in digits};
            out := +/[[d := random dset, dset less:= d](1) : until dset = {}];
        end loop;
        return out;
    end proc;
end program;
