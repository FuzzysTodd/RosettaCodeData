DECLARE SUB padovanN (n!, p!())
CONST t = 15
DIM SHARED p(t)

PRINT "First"; t; " terms of the Padovan n-step number sequences:"
FOR n = 2 TO 8
    PRINT n; ":";

    CALL padovanN(n, p())

    FOR i = 0 TO t - 1
        PRINT USING "### "; p(i);
    NEXT i
    PRINT
NEXT n

SUB padovanN (n, p())
    IF n < 2 OR t < 3 THEN
        FOR i = 0 TO t - 1
            p(i) = 1
        NEXT i
        EXIT SUB
    END IF

    CALL padovanN(n - 1, p())

    FOR i = n + 1 TO t - 1
        p(i) = 0
        FOR j = i - 2 TO i - n - 1 STEP -1
            p(i) = p(i) + p(j)
        NEXT j
    NEXT i
END SUB
