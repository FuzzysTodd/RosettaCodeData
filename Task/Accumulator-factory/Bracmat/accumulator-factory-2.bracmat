( ( accumulator
  =
    .
      ' ( add sum object addFunction
        .   ( addFunction
            =   A B
              .   !arg:(?A.?B)
                & (   !A:#
                    & !B:#
                    & "If both values are recognized as integer or fractional values, just use '+'."
                    & !A+!B
                  |  "Otherwise, create an object for adding two C doubles and let that run."
                    &  (   new
                        $ (UFP,'(.$($A)+$($B)))
                      . go
                      )
                    $
                  )
            )
          & ( object
            = add
            = addFunction$($arg.!arg)
            )
          & !(object.add):?sum
          &   'addFunction$($($sum).!arg)
            : (=?(object.add))
          & !sum
        )
  )
& accumulator$1:(=?x)
& x$5
& accumulator$1:(=?y)
& y$"5.0"
& out$(x$23/10)
& out$(y$"2.3")
)
