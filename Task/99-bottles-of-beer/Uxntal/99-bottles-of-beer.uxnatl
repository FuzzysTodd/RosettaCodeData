( uxncli 99bottles.rom )

|10 @Console &vector $2 &read $1 &pad $4 &type $1 &write $1 &error $1

|0100 ( -> )
	#63 &loop
		DUP <print-verse>
		[ LIT2 0a -Console/write ] DEO
		#01 EQUk ?&done
		POP #01 SUB
	!&loop
	&done BRK

@<print-verse> ( num -- )
	DUP <print-bottle> ;dict/wall <print-string>
	DUP <print-bottle> [ LIT2 0a -Console/write ] DEO
	;dict/take <print-string>
	#01 SUB <print-bottle> ;dict/wall !<print-string>

@<print-bottle> ( num -- )
	DUP #00 EQU ?&zero
	DUP #01 EQU ?&one
	<print-dec> ;dict/bottle <print-string>
	[ LIT2 "s -Console/write ] DEO
	!&end

	&one ( num -- )
		<print-dec> ;dict/bottle <print-string>
		!&end
	&zero ( num -- )
		POP ;dict/no-more <print-string>
		;dict/bottle <print-string>
		[ LIT2 "s -Console/write ] DEO
		( >> )
	&end
		;dict/of-beer
		( >> )

@<print-string> ( str -- )
	&loop
		LDAk .Console/write DEO
		INC2 LDAk ?&loop
	POP2 JMP2r

@<print-dec> ( byte -- )
	DUP #64 DIV <print-num>/try
	DUP #0a DIV <print-num>/try
	( >> )

@<print-num> ( num -- )
	#0a DIVk MUL SUB
	[ LIT "0 ] ADD .Console/write DEO
	JMP2r

	&try ( num -- )
		DUP ?<print-num>
		POP JMP2r

@dict &no-more "No 20 "more $1
	&bottle 20 "bottle $1
	&of-beer 20 "of 20 "beer 20 $1
	&wall "on 20 "the 20 "wall 0a $1
	&take "Take 20 "one 20 "down, 20 "pass 20 "it 20 "around 0a $1
