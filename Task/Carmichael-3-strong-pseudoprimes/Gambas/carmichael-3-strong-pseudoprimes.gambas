Public isprime[1000000] As Integer

Public Sub Main()

  Dim max_sieve As Integer = 1000000
  Dim i As Integer, j As Integer

  'set up sieve
  For i = 3 To max_sieve Step 2
    isprime[i] = 1
  Next

  isprime[2] = 1
  For i = 3 To Sqr(max_sieve) Step 2
    If isprime[i] = 1 Then
      For j = i * i To max_sieve Step i * 2
        isprime[j] = 0
      Next
    End If
  Next

  For i = 2 To 61
    If isprime[i] <> 0 Then carmichael3(i)
  Next

End

Sub carmichael3(p1 As Integer)

  Dim h3 As Integer, d As Integer
  Dim p2 As Integer, p3 As Integer, t1 As Integer, t2 As Integer

  For h3 = 1 To p1 - 1
    t1 = (h3 + p1) * (p1 - 1)
    t2 = (-p1 * p1) Mod h3
    If t2 < 0 Then t2 = t2 + h3
    For d = 1 To h3 + p1 - 1
      If t1 Mod d = 0 And t2 = (d Mod h3) Then
        p2 = 1 + (t1 \ d)
        If isprime[p2] = 0 Then Continue
        p3 = 1 + ((p1 * p2) \ h3)
        If isprime[p3] = 0 Or ((p2 * p3) Mod (p1 - 1)) <> 1 Then Continue
        Print Format$(p1, "###"); " * "; Format$(p2, "####"); " * "; Format$(p3, "#####")
      End If
    Next
  Next

End Sub
