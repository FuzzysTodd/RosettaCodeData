$ENTRY Go {
    = <Show 1 <Walk 1 <Doors>>>;
};

NDoors { = 100; };
Doors  { = <Repeat <NDoors> Closed>; };

Repeat {
    0   s.val = ;
    s.N s.val = s.val <Repeat <- s.N 1> s.val> ;
};

Toggle {
    1   Closed e.rest = Open   e.rest;
    1   Open   e.rest = Closed e.rest;
    s.N s.door e.rest = s.door <Toggle <- s.N 1> e.rest>;
};

Pass {
    s.pass s.door e.doors, <Compare s.door <NDoors>>: '+'
        = e.doors;
    s.pass s.door e.doors
        = <Pass s.pass <+ s.pass s.door> <Toggle s.door e.doors>>;
};

Walk {
    s.pass e.doors, <Compare s.pass <NDoors>>: '+'
        = e.doors;
    s.pass e.doors
        = <Walk <+ s.pass 1> <Pass s.pass s.pass e.doors>>;
};

Show {
    s.N Open   e.rest = <Prout Door s.N is open>
                        <Show <+ s.N 1> e.rest>;
    s.N Closed e.rest = <Show <+ s.N 1> e.rest>;
    s.N = ;
};
