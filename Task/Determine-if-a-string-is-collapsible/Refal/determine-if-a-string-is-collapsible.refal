$ENTRY Go {
    , ('')
      ('"If I were two-faced, would I be wearing this '
       'one?" --- Abraham Lincoln ')
      ('..11111111111111111111111111111111111111111111'
       '11111111111111111117777888')
      ('I never give \'em hell, I just tell the truth, '
       'and they think it\'s hell. ')
      ('                                              '
       '      --- Harry S Truman  '): e.Strings
    = <Each Show e.Strings>;
};

Each {
    s.F = ;
    s.F t.I e.X = <Mu s.F t.I> <Each s.F e.X>;
};

Brackets {
    e.X, <Lenw e.X>: s.L e.X =
        <Prout <Symb s.L> ': <<<' e.X '>>>'>;
};

Show {
    (e.X) = <Brackets e.X>
            <Brackets <Collapse e.X>>
            <Prout>;
};

Collapse {
    = ;
    s.C s.C e.S = <Collapse s.C e.S>;
    s.C e.S = s.C <Collapse e.S>;
};
