100H: /* FIND THE SMALLEST M WHERE M*N = DIGIT SUM OF N, N IN 1 .. 70         */

   /* CP/M BDOS SYSTEM CALL AND I/O ROUTINES                                  */
   BDOS: PROCEDURE( FN, ARG ); DECLARE FN BYTE, ARG ADDRESS; GOTO 5; END;
   PR$CHAR:   PROCEDURE( C ); DECLARE C BYTE;    CALL BDOS( 2, C );  END;
   PR$STRING: PROCEDURE( S ); DECLARE S ADDRESS; CALL BDOS( 9, S );  END;
   PR$NL:     PROCEDURE;   CALL PR$CHAR( 0DH ); CALL PR$CHAR( 0AH ); END;
   PR$NUMBER: PROCEDURE( N ); /* PRINTS A NUMBER IN THE MINIMUN FIELD WIDTH   */
      DECLARE N ADDRESS;
      DECLARE V ADDRESS, N$STR ( 6 )BYTE, W BYTE;
      V = N;
      W = LAST( N$STR );
      N$STR( W ) = '$';
      N$STR( W := W - 1 ) = '0' + ( V MOD 10 );
      DO WHILE( ( V := V / 10 ) > 0 );
         N$STR( W := W - 1 ) = '0' + ( V MOD 10 );
      END;
      CALL PR$STRING( .N$STR( W ) );
   END PR$NUMBER;

   /* TASK                                                                    */

   DECLARE TRUE LITERALLY '0FFH', FALSE LITERALLY '0';
   DIGIT$SUM: PROCEDURE( N )ADDRESS;            /* RETURNS THE DIGIT SUM OF N */
      DECLARE N ADDRESS;
      IF  N < 10 THEN RETURN N;
      ELSE DO;
         DECLARE ( RESULT, V ) ADDRESS;
         RESULT = N MOD 10;
         V      = N  /  10;
         DO WHILE V > 0;
            RESULT = RESULT + ( V MOD 10 );
            V      =            V  /  10;
         END;
         RETURN RESULT;
      END;
   END DIGIT$SUM ;

   /* SHOW THE MINIMUM MULTIPLE OF N WHERE THE DIGIT SUM OF THE MULTIPLE IS N */
   DECLARE ( M, N ) ADDRESS;
   DECLARE FOUND$MULTIPLE BYTE;
   DO N = 1 TO 39;
      FOUND$MULTIPLE = FALSE;
      M = 0;
      DO WHILE NOT FOUND$MULTIPLE;
         M = M + 1;
         IF DIGIT$SUM( M * N ) = N THEN DO;
            FOUND$MULTIPLE = TRUE;
            CALL PR$CHAR( ' ' );
            IF M < 10000 THEN DO;
               CALL PR$CHAR( ' ' );
               IF M < 1000 THEN DO;
                  CALL PR$CHAR( ' ' );
                  IF M < 100 THEN DO;
                     CALL PR$CHAR( ' ' );
                     IF M < 10 THEN CALL PR$CHAR( ' ' );
                  END;
               END;
            END;
            CALL PR$NUMBER( M );
            IF N MOD 8 = 0 THEN CALL PR$NL;
         END;
      END;
   END;

EOF
