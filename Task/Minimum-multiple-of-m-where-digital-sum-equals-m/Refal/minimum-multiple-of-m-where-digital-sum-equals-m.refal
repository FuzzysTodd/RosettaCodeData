$ENTRY Go {
    = <Table MinMult 1 70 10 10>;
};

MinMult {
    s.N = <MinMult s.N 1>;
    s.N s.M, <DigSum <* s.M s.N>>: s.N = s.M;
    s.N s.M = <MinMult s.N <+ s.M 1>>;
};

DigSum {
    0 = 0;
    s.N, <Symb s.N>: s.D e.R = <+ <Numb s.D> <DigSum <Numb e.R>>>;
};

Cell {
    s.Size e.X, <Lenw e.X>: s.Cur e.Y,
                <Compare s.Cur s.Size>: '-' = <Cell s.Size ' 'e.X>;
    s.Size e.X = e.X;
};

Table {
    s.F s.N s.Max s.Width s.CW =
        <Table s.F s.N s.Max s.Width s.CW s.Width ()>;
    s.F s.N s.Max s.Width s.CW 0 (e.Line) =
        <Prout e.Line>
        <Table s.F s.N s.Max s.Width s.CW s.Width ()>;
    s.F s.N s.Max s.Width s.CW s.Col (e.Line), <Compare s.N s.Max>: '+' =
        <Prout e.Line>;
    s.F s.N s.Max s.Width s.CW s.Col (e.Line) =
        <Table s.F <+ s.N 1> s.Max s.Width s.CW <- s.Col 1>
         (e.Line <Cell s.CW <Symb <Mu s.F s.N>>>)>;
};
