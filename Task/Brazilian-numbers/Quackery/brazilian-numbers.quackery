  [ dup base put
    /mod temp put
    true swap
    [ dup 0 > while
      base share /mod
      temp share != iff
        [ dip not ]
        done
      again ]
    drop
    temp release
    base release ]      is allsame   ( n n --> b )

  [ false swap
    dup 3 - times
      [ dup i 2 +
        allsame iff
          [ dip not
            conclude ]
          done ]
    drop ]              is brazilian (   n --> b )

  say "First 20 Brazilian numbers:" cr
  [] 0
  [ dup brazilian if
      [ dup dip join ]
    1+
    over size 20 = until ]
  drop echo
  cr
  cr
  say "First 20 odd Brazilian numbers:" cr
  [] 1
  [ dup brazilian if
      [ dup dip join ]
    2 +
    over size 20 = until ]
  drop echo
  cr
  cr
  say "First 20 prime Brazilian numbers:" cr
  [] 1
  [ dup isprime not iff
      [ 2 + ] again
    dup brazilian if
      [ dup dip join ]
    2 +
    over size 20 = until ]
  drop echo
