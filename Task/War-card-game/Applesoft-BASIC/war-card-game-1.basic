 100 R =  RND (0): REM SEED
 110 W = 1: REM  BICYCLE RULE: ONLY ONE EXTRA CARD GIVEN UP IN WAR
 120 P = 2: REM  BICYCLE RULE: ONLY TWO PLAYERS
 130 D = 1: REM  BICYCLE RULE: ONLY ONE DECK OF CARDS
 140  DIM D$(P),C$(P),G(P),L(P)
 150  SUIT$ = "CDHS"
 160  FACE$ = "23456789TJQKA"
 170  M =  LEN (SUIT$)
 180  FOR I = 1 TO D: FOR S = 1 TO M: FOR F = 1 TO  LEN (FACE$):DECK$ = DECK$ +  CHR$ ((F - 1) * M + (S - 1)): NEXT F,S,I
 190  TEXT : HOME : POKE 34,12
 REM INITIALIZE
 200  N =  LEN (DECK$)
 210  PRINT "SHUFFLING ..."
 220  FOR I = 1 TO N
 230      R =  INT ( RND (1) * N + 1)
 240      R$ =  MID$ (DECK$,R,1)
 250      C$ =  MID$ (DECK$,I,1)
 260      DECK$ =  MID$ (DECK$,1,I - 1) + R$ +  MID$ (DECK$,I + 1)
 270      DECK$ =  MID$ (DECK$,1,R - 1) + C$ +  MID$ (DECK$,R + 1)
 280  NEXT
 REM DEAL TO PLAYERS
 300  E =  INT (N / P)
 310  FOR I = 1 TO P
 320      D$(I) =  MID$ (DECK$,(I - 1) * E + 1,E)
 330  NEXT
 340  P$ =  MID$ (DECK$,(I - 1) * E + 1): REM WINNER OF THE FIRST PLAY KEEPS THESE REMAINING CARDS
 350  P$ = "": REM REMOVE REMAINING CARDS FROM THE GAME
 REM PLAY
 400  FOR T = 0 TO 1E38
 410  GOSUB 500"TURN
 420  IF Q = 0 THEN  NEXT T
 430  PRINT  TAB( 10)"IN "T" TURNS": TEXT : VTAB 11: PRINT : CALL  - 868: PRINT "...": VTAB 23
 440  END

 REM TURN
 500  GOSUB 800"IS GAME OVER?
 510  IF Q THEN  RETURN
 520  U = 0: REM UTMOST CARD
 530  C = 0: REM COUNT THE PLAYERS WITH THE UTMOST CARD
 540  FOR I = 1 TO P
 550      C$(I) =  MID$ (D$(I),1,1)
 560      IF  LEN (C$(I)) AND (R OR  NOT L(I)) THEN  GOSUB 700"draw card
 570  NEXT I
 580  IF C = 1 THEN  PRINT "PLAYER "A" TAKES THE CARDS.": FOR I = 1 TO P:L(I) = 0:D$(A) = D$(A) + C$(I): NEXT I:D$(A) = D$(A) + P$:P$ = "": RETURN
 590  FOR I = 1 TO P:L(I) = G(I) <  > C: NEXT I
 600  PRINT "TIE!"
 610  GOSUB 800"IS GAME OVER?
 620  IF Q THEN  RETURN
 630  FOR I = 1 TO P:P$ = P$ + C$(I): NEXT I
 640  FOR I = 1 TO P
 REM      DOES NOT APPLY TO 2 PLAYER GAMES (BICYCLE RULE): R MEANS IGNORE THE RULE THAT ONLY THE WINNERS PLACE CARD(S) FACE DOWN
 650      IF R OR  NOT L(I) THEN  FOR J = 1 TO W:P$ = P$ +  MID$ (D$(I),1,1):D$(I) =  MID$ (D$(I),2): PRINT " ? ";: NEXT J
 660  NEXT I
 670  PRINT "CARDS ARE FACE DOWN."
 680  RETURN

 REM DRAW CARD
 700  G =  ASC (C$(I))
 710  G(I) =  INT (G / M) + 1
 720  PRINT  MID$ (FACE$,G(I),1) MID$ (SUIT$,G - (G(I) - 1) * M + 1,1)" ";
 730  D$(I) =  MID$ (D$(I),2)
 740  C = C + (G(I) = U)
 750  IF G(I) > U THEN U = G(I):C = 1:A = I
 760  RETURN

 REM IS GAME OVER?
 800  C = 0
 810  FOR I = 1 TO P
 820      IF  LEN (D$(I)) THEN A = I:C = C + 1
 830  NEXT I
 840  IF C > 1 THEN  RETURN
 REM GAME OVER - WHO WON MESSAGE
 850  Q = 1
 860  IF C THEN  PRINT "PLAYER "A" WINS THE GAME.": RETURN
 870  PRINT "GAME ENDS AS A TIE.": RETURN
