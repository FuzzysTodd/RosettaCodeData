  [ ' [ 1 3 7 ]
    ' [ 2 4 5 6 ] ]      is initialise (       --> r s   )

  [ over size 1 -
    over swap peek
    dip [ over -1 peek ]
    + swap dip join
    over -2 split nip do
    temp put
    1 + from
      [ temp share
        index = iff
          end done
        index join ]
    temp release ]       is extend     (   r s --> r s   )

  [ temp put
    [ over size
      temp share < while
      extend again ]
    over
    temp take 1 - peek ] is ffr        ( r s n --> r s n )

  [ temp put
    [ dup size
      temp share < while
      extend again ]
    dup
    temp take 1 - peek ] is ffs        ( r s n --> r s n )

  initialise
  say "R(1)..R(10): "
  10 times
    [ i^ 1+ ffr echo sp ]
  cr cr
  960 ffs drop
  960 split drop
  dip [ 40 split drop ]
  join sort
  [] 1000 times
    [ i^ 1+ join ]
  !=
  say "All integers from 1 to 1000"
  if say " not"
  say " found once and only once."
