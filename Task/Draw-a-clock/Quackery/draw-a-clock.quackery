  [ $ "turtleduck.qky" loadfile
    $ "bigrat.qky"     loadfile ] now!

  [ $ \
import datetime
sec = datetime.timedelta(seconds=1)
time_will_be = datetime.datetime.now()+sec
hours = time_will_be.hour
minutes = time_will_be.minute
seconds = time_will_be.second
to_stack([hours, minutes, seconds])
\ python ]                                 is time+1 (   --> [ )


  [ $ \
import time
current_time = time.time()
time_to_sleep = 1.0 - (current_time % 1.0)
time.sleep(time_to_sleep)
\ python ]                                 is wait   (   -->   )

  [ 3 wide
    60 times
      [  240 1 fly
          10 1 walk
        -250 1 fly
          1 60 turn ]
    7 wide
    12 times
      [  235 1 fly
          12 1 walk
        -247 1 fly
          1 12 turn ]
    9 wide
    4 times
      [  233 1 fly
          14 1 walk
        -247 1 fly
           1 4 turn ]
    1 wide
    ' [ 0 0 0  ] fill
      [ 10 1 circle ] ]                    is face   (   -->   )

  [ 12 wide
    unpack rot dip
     [ 43200 rot
       720 v+ ]
    12 v+
    2dup turn
    175 1 walk
    -175 1 fly
    -v turn ]                              is hour   ( [ -->   )

  [ 8 wide
    unpack rot drop
    3600 rot
    60 v+
    2dup turn
    200 1 walk
    -200 1 fly
    -v turn ]                              is minute ( [ -->   )

  [ 4 wide
    2 peek
    dup 60 turn
    225 1 walk
    -225 1 fly
    negate 60 turn ]                       is second ( [ -->   )

  [ turtle
    0 frames
    [ clear -1 4 turn
      face
      time+1 dup dup
      second minute hour
      wait
      frame 1 4 turn again ] ]             is clock  (   -->   )
