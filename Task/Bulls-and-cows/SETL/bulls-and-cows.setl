program bulls_and_cows;
    setrandom(0);

    print("Bulls and cows");
    print("--------------");
    print;

    secret := make_secret();

    loop do
        guess := read_guess();
        tries +:= 1;
        bulls := count_bulls(guess, secret);
        cows := count_cows(guess, secret);
        print(bulls, "bulls,", cows, "cows.");
        if bulls = 4 then
            print("You win!  Tries:", tries);
            exit;
        end if;
    end loop;

    proc make_secret();
        digits := [];
        loop for i in [1..4] do
            loop until not digit in digits do
                digit := 1 + random(8);
            end loop;
            digits with:= digit;
        end loop;
        return digits;
    end proc;

    proc read_guess();
        loop do
            putchar("Guess? ");
            flush(stdout);
            guess := getline(stdin);
            if exists d in guess | not d in "123456789" then
                print("invalid input:", d);
            elseif #guess /= #{d : d in guess} then
                print("no duplicates allowed");
            elseif #guess /= 4 then
                print("need 4 digits");
            else
                exit;
            end if;
        end loop;
        return [val d : d in guess];
    end proc;

    proc count_bulls(guess, secret);
        return #[i : i in [1..4] | guess(i) = secret(i)];
    end proc;

    proc count_cows(guess, secret);
        return #[d : d in guess | d in secret] - count_bulls(guess, secret);
    end proc;
end program;
