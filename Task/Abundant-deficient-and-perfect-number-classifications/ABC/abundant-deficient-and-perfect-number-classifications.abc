PUT 0 IN deficient
PUT 0 IN perfect
PUT 0 IN abundant

HOW TO FIND PROPER DIVISOR SUMS UP TO limit:
    SHARE p
    PUT {} IN p
    FOR i IN {0..limit}: PUT 0 IN p[i]
    FOR i IN {1..floor (limit/2)}:
        PUT i+i IN j
        WHILE j <= limit:
            PUT p[j]+i IN p[j]
            PUT j+i IN j

HOW TO CLASSIFY n:
    SHARE deficient, perfect, abundant, p
    SELECT:
        p[n] < n: PUT deficient+1 IN deficient
        p[n] = n: PUT perfect+1 IN perfect
        p[n] > n: PUT abundant+1 IN abundant

PUT 20000 IN limit
FIND PROPER DIVISOR SUMS UP TO limit
FOR n IN {1..limit}: CLASSIFY n

WRITE deficient, "deficient"/
WRITE perfect, "perfect"/
WRITE abundant, "abundant"/
