100 dim n$(31)
110 restore 300
120 for c = 0 to 31
130   read a$
140   n$(c) = a$
150 next c
160 dim d(32)
170 restore 380
180 for c = 0 to 32
190   read b
200   d(c) = b
210 next c
220 for i = 0 to 32
230   j = int((d(i)+5.625)/11.25)
240   if j > 31 then j = j-32
250   print using "####.##";d(i);
260   print using "  ##  ";j;
270   print n$(j)
280 next i
290 end
300 data "North","North by east","North-northeast","Northeast by north"
310 data "Northeast","Northeast by east","East-northeast","East by north"
320 data "East","East by south","East-southeast","Southeast by east"
330 data "Southeast","Southeast by south","South-southeast","South by east"
340 data "South","South by west","South-southwest","Southwest by south"
350 data "Southwest","Southwest by west","West-southwest","West by south"
360 data "West","West by north","West-northwest","Northwest by west","Northwest"
370 data "Northwest by north","North-northwest","North by west","North"
380 data 0,16.87,16.88,33.75,50.62,50.63,67.5,84.37,84.38,101.25
390 data 118.12,118.13,135,151.87,151.88,168.75,185.62,185.63,202.5,219.37
400 data 219.38,236.25,253.12,253.13,270,286.87,286.88,303.75,320.62,320.63
410 data 337.5,354.37,354.38
