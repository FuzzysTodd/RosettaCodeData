program isqrt;
    loop for i in [1..65] do
        putchar(lpad(str isqrt(i), 5));
        if i mod 13=0 then print(); end if;
    end loop;

    print();
    loop for p in [1, 3..73] do
        sqrtp := isqrt(7 ** p);
        print("sqrt(7^" + lpad(str p,2) + ") = " + lpad(str sqrtp, 32));
    end loop;

    proc isqrt(x);
        q := 1;
        loop while q<=x do
            q *:= 4;
        end loop;
        z := x;
        r := 0;
        loop while q>1 do
            q div:= 4;
            t := z-r-q;
            r div:= 2;
            if t>=0 then
                z := t;
                r +:= q;
            end if;
        end loop;
        return r;
    end proc;
end program;
