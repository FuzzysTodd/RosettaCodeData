100H:
BDOS: PROCEDURE (FN,AR); DECLARE FN BYTE, AR ADDRESS; GO TO 5; END BDOS;
EXIT: PROCEDURE; GO TO 0; END EXIT;
PR$CHAR: PROCEDURE (CR); DECLARE CR BYTE; CALL BDOS(2,CR); END PR$CHAR;
PR$STR: PROCEDURE (S); DECLARE S ADDRESS; CALL BDOS(9,S); END PR$STR;

DIGITS: PROCEDURE (N,BUF) BYTE;
    DECLARE (N, BUF) ADDRESS;
    DECLARE (DIGIT BASED BUF, TEMP, I, LEN) BYTE;
    I = 5;
STEP:
    DIGIT(I := I-1) = N MOD 10;
    IF (N := N/10) > 0 THEN GO TO STEP;
    LEN = 0;
    DO WHILE I<5;
        DIGIT(LEN) = DIGIT(I);
        LEN = LEN+1;
        I = I+1;
    END;
    RETURN LEN;
END DIGITS;

PR$NUM: PROCEDURE (N);
    DECLARE N ADDRESS, DS (5) BYTE, I BYTE;
    DO I = 0 TO DIGITS(N,.DS) - 1;
        CALL PR$CHAR('0' + DS(I));
    END;
    CALL PR$STR(.(13,10,'$'));
END PR$NUM;

POWER: PROCEDURE (N,P) ADDRESS;
    DECLARE (N, P, R) ADDRESS;
    R = 1;
    DO WHILE P > 0;
        R = R * N;
        P = P - 1;
    END;
    RETURN R;
END POWER;

NARCISSIST: PROCEDURE (N) ADDRESS;
    DECLARE (LEN, I) BYTE, DS (5) BYTE;
    DECLARE (N, POWSUM) ADDRESS;
    LEN = DIGITS(N, .DS);
    POWSUM = 0;
    DO I = 0 TO LEN-1;
        POWSUM = POWSUM + POWER(DS(I), LEN);
    END;
    RETURN POWSUM = N;
END NARCISSIST;

DECLARE CAND ADDRESS;
DO CAND = 0 TO 65534;
    IF NARCISSIST(CAND) THEN CALL PR$NUM(CAND);
END;

CALL EXIT;
EOF
