100 REM Horizontal sundial calculations
110 INPUT PROMPT "Enter latitude       => ": Lat
120 INPUT PROMPT "Enter longitude      => ": Lng
130 INPUT PROMPT "Enter legal meridian => ": Ref
140 PRINT
150 OPTION ANGLE DEGREES
160 LET Slat = SIN(Lat)
170 PRINT "    sine of latitude:   "; Slat
180 PRINT "    diff longitude:     "; Lng - Ref
190 PRINT
200 PRINT "Hour, sun hour angle, dial hour line angle from 6am to 6pm"
210 FOR Hour = -6 TO 6
220    LET HourAngle = 15 * Hour
230    LET HourAngle = HourAngle - (Lng - Ref) ! correct for longitude difference
240    LET HourLineAngle = ATN(Slat * TAN(HourAngle))
250    PRINT USING "HR=###; HRA=####.###; HLA=####.###": Hour, HourAngle, HourLineAngle
260 NEXT Hour
270 END
