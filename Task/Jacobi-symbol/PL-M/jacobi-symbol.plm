100H: /* JACOBI SYMBOL                                                       */

   /* CP/M BDOS SYSTEM CALLS AND I/O ROUTINES                                */
   BDOS:   PROCEDURE( FN, ARG ); DECLARE FN BYTE, ARG ADDRESS; GOTO 5; END;
   PR$CHAR:   PROCEDURE( C ); DECLARE C BYTE;    CALL BDOS( 2, C );    END;
   PR$STRING: PROCEDURE( S ); DECLARE S ADDRESS; CALL BDOS( 9, S );    END;
   PR$NL:     PROCEDURE; CALL PR$STRING( .( 0DH, 0AH, '$' ) );         END;
   PR$NUMBER: PROCEDURE( N ); /* PRINTS A NUMBER IN THE MINIMUN FIELD WIDTH  */
      DECLARE N ADDRESS;
      DECLARE V ADDRESS, N$STR ( 6 )BYTE, W BYTE;
      V = N;
      W = LAST( N$STR );
      N$STR( W ) = '$';
      N$STR( W := W - 1 ) = '0' + ( V MOD 10 );
      DO WHILE( ( V := V / 10 ) > 0 );
         N$STR( W := W - 1 ) = '0' + ( V MOD 10 );
      END;
      CALL PR$STRING( .N$STR( W ) );
   END PR$NUMBER;

   /* TASK                                                                   */

   JACOBI: PROCEDURE( A$IN, N$IN )BYTE;
      DECLARE ( A$IN, N$IN ) ADDRESS;
      IF N$IN MOD 2 <> 1 THEN DO;
         CALL PR$STRING( .'JACOBI PARAMETER NOT ODD$' );
         RETURN 0;
         END;
      ELSE DO;
         DECLARE ( A, N, NM8, T ) ADDRESS;
         DECLARE JS BYTE;
         A = A$IN MOD N$IN; N = N$IN; JS = 1;
         DO WHILE A <> 0;
            DO WHILE A MOD 2 = 0;
               A = A / 2;
               NM8 = N MOD 8;
               IF NM8 = 3 OR NM8 = 5 THEN JS = - JS;
            END;
            T = A; A = N; N = T;
            IF A MOD 4 = 3 AND N MOD 4 = 3 THEN JS = - JS;
            A = A MOD N;
         END;
         IF N = 1 THEN RETURN JS;
                  ELSE RETURN 0;
      END;
   END JACOBI ;

   DECLARE ( A, N )ADDRESS;
   DECLARE JS BYTE;

   CALL PR$STRING( .'TABLE OF JACOBI(A, N):$' );CALL PR$NL;
   CALL PR$STRING( .'N/A   1   2   3   4   5   6   7$' );
   CALL PR$STRING( .'   8   9  10  11  12  13  14  15$' );CALL PR$NL;
   CALL PR$STRING( .'-------------------------------$' );
   CALL PR$STRING( .'--------------------------------$' );CALL PR$NL;
   DO N = 1 TO 29 BY 2;
      CALL PR$CHAR( ' ' );
      IF N < 10 THEN CALL PR$CHAR( ' ' );
      CALL PR$NUMBER( N );
      DO A = 1 TO 15;
         JS = JACOBI( A, N );
         IF      JS = 0 THEN CALL PR$STRING( .'   0$' );
         ELSE IF JS = 1 THEN CALL PR$STRING( .'   1$' );
         ELSE                CALL PR$STRING( .'  -1$' );
      END;
      CALL PR$NL;
   END;

EOF
