$ENTRY Go {
    , <ArgList>: {
        = <PrintNotes>;
        e.Args = <WriteNote e.Args>;
    };
};

ArgList {
    = <ArgList 1>;
    s.N, <Arg s.N>: {
        = ;
        e.Arg = (e.Arg) <ArgList <+ s.N 1>>;
    };
};

PrintNotes {
    , <ExistFile 'notes.txt'>: {
        False = ;
        True = <PrintFile 1 'notes.txt'>;
    };
};

PrintFile {
    s.Chan e.File = <Open 'r' s.Chan e.File> <PrintFile (s.Chan)>;
    (s.Chan), <Get s.Chan>: {
        0 = <Close s.Chan>;
        e.Line = <Prout e.Line> <PrintFile (s.Chan)>;
    };
};

WriteNote {
    e.Args, <Time> '\n\t' <Join (' ') e.Args> '\n': e.Note =
        <Open 'a' 2 'notes.txt'>
        <Put 2 e.Note>
        <Close 2>;
};

Join {
    (e.X) = ;
    (e.X) (e.1) = e.1;
    (e.X) (e.1) e.2 = e.1 e.X <Join (e.X) e.2>;
};
